// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            "androidx_appcompat"       : "1.0.2",
            "androidx_core_ktx"        : "1.0.2",
            "androidx_constraintlayout": "1.1.3",
            "androidx_navigation"      : "2.0.0",

            "versionCode"              : 1,
            "versionName"              : "1.0",
            "kotlin"                   : "1.3.31",
            "gradle"                   : "3.4.2",
            "compile_sdk"              : 29,
            "min_sdk"                  : 15,
            "target_sdk"               : 29,
            "junit"                    : "4.12",
            "test_runner"              : "1.2.0",
            "espresso_core"            : "3.2.0",
            "javaVersion"              : JavaVersion.VERSION_1_8
    ]

    ext.deps = [
            "androidx_appcompat"          : "androidx.appcompat:appcompat:${versions.androidx_appcompat}",
            "androidx_core_ktx"           : "androidx.core:core-ktx:${versions.androidx_core_ktx}",
            "androidx_constraintlayout"   : "androidx.constraintlayout:constraintlayout:${versions.androidx_constraintlayout}",
            "androidx_navigation_fragment": "androidx.navigation:navigation-fragment-ktx:${versions.androidx_navigation}",
            "androidx_navigation_ui"      : "androidx.navigation:navigation-ui-ktx:${versions.androidx_navigation}",

            "gradle"                      : "com.android.tools.build:gradle:${versions.gradle}",
            "kotlin_gradle_plugin"        : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            "kotlin_stdlib"               : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",

            "junit"                       : "junit:junit:${versions.junit}",
            "test_runner"                 : "androidx.test:runner:${versions.test_runner}",
            "espresso_core"               : "androidx.test.espresso:espresso-core:${versions.espresso_core}",
    ]

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath deps.gradle
        classpath deps.kotlin_gradle_plugin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            //apply plugin: "com.github.ben-manes.versions"

            android {
                //buildToolsVersion Config.buildTools
                compileSdkVersion versions.compile_sdk

                defaultConfig {
                    minSdkVersion versions.min_sdk
                    targetSdkVersion versions.target_sdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility versions.javaVersion
                    targetCompatibility versions.javaVersion
                }
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
