// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            "androidx_appcompat"       : "1.0.2",
            "androidx_core_ktx"        : "1.0.2",
            "androidx_constraintlayout": "1.1.3",
            "androidx_navigation"      : "2.0.0",
            "androidx_room"            : "2.1.0",
            "androidx_legacy_support"  : "1.0.0",
            "androidx_lifecycle"       : "2.0.0",
            "androidxFragment":"1.1.0-rc01",

            "versionCode"              : 1,
            "versionName"              : "1.0",
            "kotlin"                   : "1.3.31",
            "gradle"                   : "3.4.2",
            "compile_sdk"              : 29,
            "min_sdk"                  : 15,
            "target_sdk"               : 29,
            "junit"                    : "4.12",
            "test_runner"              : "1.2.0",
            "espresso_core"            : "3.2.0",
            "javaVersion"              : JavaVersion.VERSION_1_8
    ]

    ext.deps = [
            "androidx_appcompat"       : "androidx.appcompat:appcompat:${versions.androidx_appcompat}",
            "androidx_core_ktx"        : "androidx.core:core-ktx:${versions.androidx_core_ktx}",
            "androidx_constraintlayout": "androidx.constraintlayout:constraintlayout:${versions.androidx_constraintlayout}",
            "androidx_legacy_support"  : "androidx.legacy:legacy-support-v4:${versions.androidx_legacy_support}",

            "junit"                    : "junit:junit:${versions.junit}",
            "test_runner"              : "androidx.test:runner:${versions.test_runner}",
            "espresso_core"            : "androidx.test.espresso:espresso-core:${versions.espresso_core}",

            'kotlin'                   : [
                    'stdlib'      : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
                    'test'        : "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin}",
                    'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            ],
            'androidxRoom'             : [
                    'runtime' : "androidx.room:room-runtime:${versions.androidx_room}",
                    'compiler': "androidx.room:room-compiler:${versions.androidx_room}",
                    'ktx'     : "androidx.room:room-ktx:${versions.androidx_room}"
            ],
            'androidxLifecycle'        : [
                    'extensions': "androidx.lifecycle:lifecycle-extensions:${versions.androidx_lifecycle}",
                    'compiler'  : "androidx.lifecycle:lifecycle-compiler:${versions.androidx_lifecycle}",
                    'viewmodel' : "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.androidx_lifecycle}"
            ],
            'androidxNavigation'       : [
                    'fragment': "androidx.navigation:navigation-fragment-ktx:${versions.androidx_navigation}",
                    'ui'      : "androidx.navigation:navigation-ui-ktx:${versions.androidx_navigation}"
            ],
            'androidxFragment'       : [
                    'fragmentKtx': "androidx.fragment:fragment-ktx:${versions.androidxFragment}"
            ]
    ]

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradle}"
        classpath deps.kotlin.gradlePlugin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            //apply plugin: "com.github.ben-manes.versions"

            android {
                //buildToolsVersion Config.buildTools
                compileSdkVersion versions.compile_sdk

                defaultConfig {
                    minSdkVersion versions.min_sdk
                    targetSdkVersion versions.target_sdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility versions.javaVersion
                    targetCompatibility versions.javaVersion
                }
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
