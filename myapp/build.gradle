// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            "versionCode"  : 1,
            "versionName"  : "1.0",

            'androidx'     : [
                    "appcompat"       : "1.0.2",
                    "core"            : "1.0.2",
                    "constraintlayout": "1.1.3",
                    "navigation"      : "2.0.0",
                    "room"            : "2.1.0",
                    "legacySupport"   : "1.0.0",
                    "lifecycle"       : "2.0.0",
                    "fragment"        : "1.1.0-rc01"
            ],

            "kotlin"       : "1.3.31",
            "gradle"       : "3.4.2",
            "compile_sdk"  : 29,
            "min_sdk"      : 15,
            "target_sdk"   : 29,
            "junit"        : "4.12",
            "test_runner"  : "1.2.0",
            "espresso_core": "3.2.0",
            "javaVersion"  : JavaVersion.VERSION_1_8
    ]

    ext.deps = [
            "junit"        : "junit:junit:${versions.junit}",
            "test_runner"  : "androidx.test:runner:${versions.test_runner}",
            "espresso_core": "androidx.test.espresso:espresso-core:${versions.espresso_core}",

            'kotlin'       : [
                    'stdlib'      : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
                    'test'        : "org.jetbrains.kotlin:kotlin-test-common:${versions.kotlin}",
                    'gradlePlugin': "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
            ],
            'androidx'     : [
                    // Core
                    'constraintlayout'   : "androidx.constraintlayout:constraintlayout:${versions.androidx.constraintlayout}",
                    'appcompat'          : "androidx.appcompat:appcompat:${versions.androidx.appcompat}",
                    'coreKtx'            : "androidx.core:core-ktx:${versions.androidx.core}",

                    // Room
                    'roomRuntime'        : "androidx.room:room-runtime:${versions.androidx.room}",
                    'roomCompiler'       : "androidx.room:room-compiler:${versions.androidx.room}",
                    'roomKtx'            : "androidx.room:room-ktx:${versions.androidx.room}",

                    // Lifecycle
                    'lifecycleExtensions': "androidx.lifecycle:lifecycle-extensions:${versions.androidx.lifecycle}",
                    'lifecycleCompiler'  : "androidx.lifecycle:lifecycle-compiler:${versions.androidx.lifecycle}",
                    'lifecycleViewmodel' : "androidx.lifecycle:lifecycle-viewmodel-ktx:${versions.androidx.lifecycle}",

                    // Navigation
                    'navigationFragment' : "androidx.navigation:navigation-fragment-ktx:${versions.androidx.navigation}",
                    'navigationUi'       : "androidx.navigation:navigation-ui-ktx:${versions.androidx.navigation}",

                    // Fragment
                    'fragmentKtx'        : "androidx.fragment:fragment-ktx:${versions.androidx.fragment}",

                    // Other
                    "legacySupport"      : "androidx.legacy:legacy-support-v4:${versions.androidx.legacySupport}"
            ]
    ]

    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.gradle}"
        classpath deps.kotlin.gradlePlugin
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()

    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            //apply plugin: "com.github.ben-manes.versions"

            android {
                //buildToolsVersion Config.buildTools
                compileSdkVersion versions.compile_sdk

                defaultConfig {
                    minSdkVersion versions.min_sdk
                    targetSdkVersion versions.target_sdk
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                }

                compileOptions {
                    sourceCompatibility versions.javaVersion
                    targetCompatibility versions.javaVersion
                }
            }
        }
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}
